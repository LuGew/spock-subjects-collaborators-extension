apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.ofg:uptodate-gradle-plugin:1.1.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
    }
}


group = 'com.blogspot.toomuchcoding'
version = '1.0.0-SNAPSHOT'
description = "Spock subjects-collaborators extension"

repositories {
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.3.8"
    compile 'junit:junit:4.12'
    compile "org.spockframework:spock-core:0.7-groovy-2.0"
    
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:2.1'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        code(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = project.properties['bintrayUser']
    key = project.properties['bintrayKey']
    publications = ['code']
    pkg {
        repo = 'com-blogspot-toomuchcoding'
        name = 'spock-subjects-collaborators-extension'
        desc = '''Spock extension that allows you to inject Collaborators into Subjects. Bases on Mockito's @InjectMocks functionality'''
        licenses = ['Apache-2.0']
        labels = ['spock']
    }
}

